allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://maven.ffmpeg-kit.org" }
    }
}

rootProject.buildDir = "../build"

subprojects {
    afterEvaluate { project ->
        if (project.plugins.hasPlugin("com.android.application") ||
            project.plugins.hasPlugin("com.android.library")) {

            project.android {
                compileSdkVersion 34
                buildToolsVersion "34.0.0"

                if (namespace == null) {
                    namespace project.group
                }

                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_17
                    targetCompatibility = JavaVersion.VERSION_17
                }

                if (project.plugins.hasPlugin("org.jetbrains.kotlin.android")) {
                    kotlinOptions {
                        jvmTarget = '17'
                    }
                }
            }
        }
    }
}

subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

subprojects {
    project.evaluationDependsOn(":app")
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
